# spawner for bees; bomb carrier as far right wave 1

let maxSecs = 15.0;
let secsBetweenSpawns = (60.0 * 2.0) / 118.0;

let speed = 60.0;
let angleLeft = 45.0;

# distance between two neighboring bees
let xOffset = 22.0;

let x1 = 10.0;
let x2 = x1 + (xOffset / 2.0);

let velLeft = <<speed, angleLeft>>;
let velRight = <<speed, flipY(angleLeft)>>;

func spawnBombBee(pos, vel, depthOffset){
    spawn(
        "bee_red",
        pos,
        vel,
        depthOffset,
        "enemy_2_3_a"
    );
}

func spawnBee(pos, vel, depthOffset){
    spawn(
        "bee",
        pos,
        vel,
        depthOffset,
        "enemy_2_3_b"
    );
}

func spawnFourBeesLeft(x){
    for(let i = 0; i < 4; i = i + 1){
        spawnBee(getTopSpawnLeft(x), velLeft, i);
        x = x + xOffset;
    }
}

func spawnFourBeesRight(x){
    for(let i = 0; i < 4; i = i + 1){
        spawnBee(getTopSpawnRight(x), velRight, i);
        x = x + xOffset;
    }
}

func spawnFourBeesRightAndBombBee(x){
    spawnBombBee(getTopSpawnRight(x), velRight, 20);
    x = x + xOffset;
    for(let i = 1; i < 4; i = i + 1){
        spawnBee(getTopSpawnRight(x), velRight, i + 9);
        x = x + xOffset;
    }
}

let secs = 0.0;
spawnFourBeesRightAndBombBee(x1);

secs = secs + secsBetweenSpawns;
secTimer(secsBetweenSpawns);

let spawnedSecondBombBee = false;
while(secs < maxSecs){
    spawnFourBeesLeft(x1);
    secs = secs + secsBetweenSpawns;
    secTimer(secsBetweenSpawns);

    if(secs < maxSecs){
        spawnFourBeesRight(x2);
        secs = secs + secsBetweenSpawns;
        secTimer(secsBetweenSpawns);
    }

    if(secs < maxSecs){
        spawnFourBeesLeft(x2);
        secs = secs + secsBetweenSpawns;
        secTimer(secsBetweenSpawns);
    }

    if(secs < maxSecs){
        if(!spawnedSecondBombBee
            & secs > (maxSecs / 2)
        ){
            spawnFourBeesRightAndBombBee(x1);
            spawnedSecondBombBee = true;
        }
        else{
            spawnFourBeesRight(x1);
        }
        secs = secs + secsBetweenSpawns;
        secTimer(secsBetweenSpawns);
    }
}