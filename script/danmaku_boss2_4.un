# big-small-big danmaku script

let maxSecs = 2.5;
let secsBetweenShots = diff(0.015, 0.010, 0.007);
let speedBase = 160.0;
let speedRange = 5.0;

func randomPrototype(){
    if(chance(0.5)){
        return "large_blue";
    }
    else{
        return "large_lblue";
    }
}

let pos = getPosition();
func spawnBullet(vel, depthOffset){
    spawn(
        randomPrototype(),
        pos,
        vel,
        depthOffset,
        "bullet_boss2_4_a"
    );
}

let secInc = 1.0 / updatesPerSecond();

let secs = 0.0;
let secsSinceLastShot = 0.0;
while(secs < maxSecs){
    while(secsSinceLastShot > secsBetweenShots){
        let speed = speedBase
            + random(-speedRange, speedRange);
        let angle = randomAngle();
        let vel = <<speed, angle>>;
        spawnBullet(vel, random(0, 100));
        secsSinceLastShot = secsSinceLastShot
            - secsBetweenShots;
    }

    secs = secs + secInc;
    secsSinceLastShot = secsSinceLastShot + secInc;
    yield;
}