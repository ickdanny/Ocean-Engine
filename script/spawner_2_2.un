# spawn fireflies from side, one at a time

let maxSecs = 13.0;

let secsBetweenSpawns = 0.25;
let secInc = 1.0 / updatesPerSecond();

let speedLow = 60;
let speedHigh = 80;

# angles for the left spawn; right should flipY
let angleLow = 5.0;
let angleHigh = 15.0;

let spawnYLow = gameHeight() - 40.0;
let spawnYHigh = gameHeight() - 20.0;

func spawnLeftFirefly(pos, vel, depthOffset){
    spawn(
        "firefly_right", # left fireflies point right
        pos,
        vel,
        depthOffset,
        "enemy_2_2"
    );
}

func spawnRightFirefly(pos, vel, depthOffset){
    spawn(
        "firefly_left", # right fireflies point left
        pos,
        vel,
        depthOffset,
        "enemy_2_2"
    );
}

func spawnRandFirefly(depthOffset){
    let speed = random(speedLow, speedHigh);
    let angle = random(angleLow, angleHigh);
    let spawnY = random(spawnYLow, spawnYHigh);
    let pos;
    if(chance(0.5)){
        pos = getLeftSpawn(spawnY);
        spawnLeftFirefly(
            pos,
            <<speed, angle>>,
            depthOffset
        );
    }
    else{
        angle = flipY(angle);
        pos = getRightSpawn(spawnY);
        spawnRightFirefly(
            pos,
            <<speed, angle>>,
            depthOffset
        );
    }
}

let numSpawns = 0;
let secsSinceLastSpawn = 0.0;
for(let secs = 0.0;
    secs < maxSecs;
    secs = secs + secInc
){
    if(secsSinceLastSpawn > secsBetweenSpawns){
        secsSinceLastSpawn = secsSinceLastSpawn
            - secsBetweenSpawns;
        spawnRandFirefly(numSpawns);
        numSpawns = numSpawns + 1;
    }
    secsSinceLastSpawn = secsSinceLastSpawn + secInc;
    yield;
}