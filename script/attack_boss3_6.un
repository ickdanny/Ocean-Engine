# automaton helpers attack script

let health = 150000;
let initWaitSecs = 0.4;

let beforeMoveSecs = 0.4;
let afterMoveSecs = 0.1;
let rLow = 30;
let rHigh = 40;
let maxSpeed = 37.5;

# spawn on 1, flag on 2
let movesBetweenSpawns = 4;

setHealth(health);

# have to use attack script to spawn automatons
let x1 = 30.0;
let x2 = 50.0;
let initSpeed = 84.4;

func spawnAutomaton(pos, vel, depthOffset){
    spawn(
        "automaton_blue", # use blue; more health
        pos,
        vel,
        depthOffset,
        "enemy_boss3_6"
    );
}

let autoInitVel = <<initSpeed, 90.0>>;
func spawnAutomatons(){
    spawnAutomaton(
        getTopSpawnLeft(x1),
        autoInitVel,
        0
    );
    spawnAutomaton(
        getTopSpawnLeft(x2),
        autoInitVel,
        1
    );
    spawnAutomaton(
        getTopSpawnRight(x2),
        autoInitVel,
        2
    );
    spawnAutomaton(
        getTopSpawnRight(x1),
        autoInitVel,
        3
    );
}

if(movesBetweenSpawns < 2){
    error("moves between spawns must be at least 2");
}

secTimer(initWaitSecs);
let moveCounter = 1; # spawn first
while(true){
    addScript("danmaku_boss3_6", 4);

    if(moveCounter == 1){
        spawnAutomatons();
    }
    else if(moveCounter == 2){
        flagUser1(); # tells automatons to leave
    }

    moveCounter = moveCounter - 1;
    if(moveCounter == 0){
        moveCounter = movesBetweenSpawns;
    }

    secTimer(beforeMoveSecs);
    boundGotoDecelerating(
        bossXLow(),
        bossXHigh(),
        bossYLow(),
        bossYHigh(),
        rLow,
        rHigh,
        maxSpeed
    );
    secTimer(afterMoveSecs);
}