# spawns rain of medium stars

let secs = 7.0;
let secInc = 1.0 / updatesPerSecond();
let secsBeforeNewStar = 0.5;

# spawns become more frequent over time
let secsBeforeNewStarDecrement = 0.01;

let speedLow = 40.0;
let speedHigh = 60.0;

let angleBase = 90.0;
let angleRange = 5.0;

let leftRange = 0;
let rightRange = gameWidth();

func spawnMediumStar(pos, vel, depthOffset){
    spawn(
        "star_medium",
        pos,
        vel,
        depthOffset,
        "enemy_1_2"
    );
}

let spawnCount = 0;
let secTotal = 0.0;
let secBetweenSpawns = 0.0;
let spawnPos = [0, 0];
let spawnVel = <<0, 0>>;
while(secTotal < secs){
    if(secBetweenSpawns > secsBeforeNewStar){
        spawnVel.r = random(speedLow, speedHigh);
        spawnVel.t = angleBase
            + random(-angleRange, angleRange);

        spawnPos = getTopSpawnLeft(random(
            leftRange,
            rightRange
        ));

        spawnMediumStar(
            spawnPos,
            spawnVel,
            spawnCount
        );

        spawnCount = spawnCount + 1;
        secBetweenSpawns = secBetweenSpawns
            - secsBeforeNewStar;
        if(secsBeforeNewStar
            > secsBeforeNewStarDecrement
        ){
            secsBeforeNewStar = secsBeforeNewStar
                - secsBeforeNewStarDecrement;
        }
    }
    secTotal = secTotal + secInc;
    secBetweenSpawns = secBetweenSpawns + secInc;
    yield;
}