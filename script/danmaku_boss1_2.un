# mega tris from top danmaku script

let bulletSymmetry = 3;
let bulletInitSpeed = 10.0;
let bulletDistFromSpawn = 13.0;

let baseAngle = 90.0;
let angleRange = 20.0;

let secsBetweenSpawns = diff(0.5, 0.25, 0.12);
let spawnsBetweenB = 5;

let triSpawnVelA = <<0, 0>>;
let triSpawnPosA = [0,0];
func spawnTriA(spawnOffset, depthOffset){
    spawn(
        "tri_orange",
        triSpawnPosA + spawnOffset,
        triSpawnVelA,
        depthOffset,
        "bullet_boss1_1_a" # reuse bullet script
    );
}

let triSpawnVelB = <<0, 0>>;
func spawnTriB(spawnOffset, depthOffset){
    spawn(
        "tri_lyellow",
        getPosition() + spawnOffset,
        triSpawnVelB,
        depthOffset + 30,
        "bullet_boss1_1_b" # reuse bullet script
    );
}

func spawnThreeTrisA(){
    let x = random(0.0, gameWidth());
    triSpawnPosA = getTopSpawnLeft(x);
    let angle = baseAngle
        + random(-angleRange, angleRange);
    triSpawnVelA = <<bulletInitSpeed, angle>>;
    let baseOffset = <<bulletDistFromSpawn, angle>>;
    ring(baseOffset, bulletSymmetry, spawnTriA);
}

func spawnThreeTrisB(){
    let angle = getAngleToPlayer();
    triSpawnVelB = <<bulletInitSpeed, angle>>;
    let baseOffset = <<bulletDistFromSpawn, angle>>;
    ring(baseOffset, bulletSymmetry, spawnTriB);
}

let spawnsSinceLastB = 0;
while(true){
    spawnThreeTrisA();

    if(spawnsSinceLastB >= spawnsBetweenB){
        spawnsSinceLastB
            = spawnsSinceLastB - spawnsBetweenB;
        spawnThreeTrisB();
    }
    spawnsSinceLastB = spawnsSinceLastB + 1;

    secTimer(secsBetweenSpawns);
}