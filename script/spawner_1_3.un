# spawns large stars with demarcation

let secs = 9.0;
let secInc = 1.0 / updatesPerSecond();
let secsBeforeNewStar = 0.8;

# spawns become more frequent over time
let secsBeforeNewStarDecrement = 0.02;

let speedLow = 50.0;
let speedHigh = 70.0;

let angleBase = 90.0;
let angleRange = 2.0;

let rangeInside = 20;
let leftRange = rangeInside;
let rightRange = gameWidth() - rangeInside;

func spawnLargeStar(pos, vel, depthOffset){
    spawn(
        "star_large",
        pos,
        vel,
        depthOffset,
        "enemy_1_3" # todo: this script slow and shoot
    );
}

let spawnCount = 0;
let secTotal = 0.0;
let secBetweenSpawns = 0.0;
let spawnPos = [0, 0];
let spawnVel = <<0, 0>>;
while(secTotal < secs){
    if(secBetweenSpawns > secsBeforeNewStar){
        spawnVel.r = random(speedLow, speedHigh);
        spawnVel.t = angleBase
            + random(-angleRange, angleRange);

        spawnPos = getTopSpawnLeft(random(
            leftRange,
            rightRange
        ));

        spawnLargeStar(
            spawnPos,
            spawnVel,
            spawnCount
        );

        spawnCount = spawnCount + 1;
        secBetweenSpawns = secBetweenSpawns
            - secsBeforeNewStar;
        if(secsBeforeNewStar
            > secsBeforeNewStarDecrement
        ){
            secsBeforeNewStar = secsBeforeNewStar
                - secsBeforeNewStarDecrement;
        }
    }
    secTotal = secTotal + secInc;
    secBetweenSpawns = secBetweenSpawns + secInc;
    yield;
}